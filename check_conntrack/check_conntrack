#!/usr/bin/perl
#
#    check_conntrack nagios plugin
#    Copyright (C) 2016 Nicolas Limage
#
#    This program is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 3 of the License, or
#    (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
use strict;
use warnings;
use 5.008;
#use lib '/usr/lib/nagios/plugins/lib';
use Nagios::Plugin;

my $np = Nagios::Plugin->new(
	shortname => 'CONNTRACK',
	usage => "usage: check_conntrack <options>\n   use --help for more info",
	plugin => 'CONNTRACK',
	version => '1.0'
);

#----------------------------------------
# Option Parsing
#----------------------------------------

$np->add_arg(
	spec => 'warning|w=s',
	help => "-w, --warning=INTEGER:INTEGER\n   "
              . "warning threshold for the number of connections\n "
              . "See http://nagiosplug.sourceforge.net/"
              . "developer-guidelines.html#THRESHOLDFORMAT "
              . "for the threshold format. ",
);

$np->add_arg(
	spec => 'critical|c=s',
	help => "-c, --critical=INTEGER:INTEGER\n   "
              . "critical threshold for the number of connections\n "
              . "See http://nagiosplug.sourceforge.net/"
              . "developer-guidelines.html#THRESHOLDFORMAT "
              . "for the threshold format. ",
);

$np->getopts();

# Safety Net
alarm $np->opts->timeout;

#----------------------------------------
# Probing Values
#----------------------------------------

my $value = `conntrack -C`;
$value =~ m/^\d+$/ or $np->nagios_die("invalid value: $value");
$value = int($value);

#----------------------------------------
# Health Checks
#----------------------------------------

my $code = $np->check_threshold(check => $value,
                                warning => $np->opts->warning,
                                critical => $np->opts->critical);
$np->add_message($code, sprintf("current connections: %d", $value));

#----------------------------------------
# Performance Data
#----------------------------------------

$np->add_perfdata(label => "connections", value => $value);

#----------------------------------------
# Finish
#----------------------------------------

my ($exit_code, $exit_message) = $np->check_messages();
$np->nagios_exit($exit_code, $exit_message);
